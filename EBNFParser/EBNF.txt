Program ==> CodeBlock
CodeBlock ==> "{" { Statement } "}"
Statement ==> Declaration | Assignment | ASM | Conditional
Declaration ==> IntDeclaration | BoolDeclaration
IntDeclaration ==>"int" {"'"} Label ";"
BoolDeclaration ==> "bool" {"'"} Label ";"
Assignment ==> IntAssignment | BoolAssignment
IntAssignment ==> Label "=" IntExpression ";"
BoolAssignment ==> Label "=" BoolExpression ";"
IntExpression ==> Primitive {BinaryOperator Primitive}
Primitive ==> Integer | "(" IntExpression ")" | UnaryOperator Primitive | Label
BinaryOperator ==> "+" | "-" | "*" | "/"
UnaryOperator ==> "-"
BooleanValue ==> "true" | "false"
BoolExpression ==> BoolSubExpression { BooleanConjunction BoolSubExpression }
BoolSubExpression ==> BooleanValue | Primitive BooleanOperator Primitive | "(" BoolExpression ")"
BooleanOperator ==> "==" | "!=" | ">" | "<" | ">=" | "<="
BooleanConjunction ==> "&&" | "||"
Conditional ==> "if" "(" BoolExpression ")" CodeBlock
Label ==> Regex^[a-zA-Z_]\w+$
Integer ==> Regex^[0-9]+$
ASM ==> "<ASM>" ASMContent "</ASM>"
ASMContent ==> Regex^((?!</ASM>).)*$